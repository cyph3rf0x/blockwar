1000 CLS 0 : XO=8 : YO=10 : C=3 : K$="A" :W=0 : GOSUB 10000 : GOSUB 6000 : GOSUB 7000 : GOSUB 2000 : GOTO 4000
2000 SET(X+XO,Y+YO,C) : SET(X+XO+1,Y+YO,C) : SET(X+XO,Y+YO+1,C) : SET(X+XO+1,Y+YO+1,C) : RETURN
3000 RESET(X+XO,Y+YO) : RESET(X+XO+1,Y+YO) : RESET(X+XO,Y+YO+1) : RESET(X+XO+1,Y+YO+1) : RETURN
4000 K$=INKEY$ :B=B+1
4020 IF K$="W" THEN IF Y+YO-1>7 THEN GOSUB 3000 : LET YO=YO-1 : GOSUB 2000
4030 IF K$="S" THEN IF Y+YO+1<28 THEN GOSUB 3000 : LET YO=YO+1 : GOSUB 2000
4040 IF K$="A" THEN IF X+XO-1>3 THEN GOSUB 3000 : LET XO=XO-4 : GOSUB 2000
4050 IF K$="D" THEN IF X+XO+1<55 THEN GOSUB 3000 : LET XO=XO+4 : GOSUB 2000
4060 IF K$="L" THEN GOSUB 5000 : GOSUB 9000
4070 IF K$="1" THEN W=0:GOTO 1000
4080 IF W=10 THEN GOTO 10100
4090 IF B=10 THEN LET B=0 : GOSUB 9000 : GOTO 4000
4100 GOTO 4000
5000 FOR F=0 TO 20
5011 IF X+XO+F+4 > 60 THEN GOTO 5050
5020 IF POINT(X+XO+F+4,Y+YO) > 0 THEN W=W+1:PRINT@0, "SCORE:"; W : GOSUB 8000 : GOSUB 5500
5030 SET(X+XO+F+4,Y+YO,2)
5040 NEXT F
5050 SOUND 128,1
5060 FOR S=0 TO 20
5065 IF X+XO+S+4 > 60 THEN GOTO 5090
5070 RESET(X+XO+S+4,Y+YO)
5080 NEXT S
5090 SOUND 192,1
5100 RETURN
5500 IF X+XO+F+4=JX AND Y+YO=JY THEN RESET(JX,JY): JF=1 : JX=RND(30)+25: JY=RND(15)+10: SET(JX,JY,4)
5520 IF X+XO+F+4=KX AND Y+YO=KY THEN RESET(KX,KY): KF=1 : KX=RND(30)+25: KY=RND(15)+10: SET(KX,KY,4)
5530 IF X+XO+F+4=LX AND Y+YO=LY THEN RESET(LX,LY): LF=1 : LX=RND(30)+25: LY=RND(15)+10: SET(LX,LY,4)
5600 RETURN
6000 JX=RND(30)+25: JY=RND(15)+10 : SET(JX,JY,4)
6001 KX=RND(30)+25: KY=RND(15)+10 : SET(KX,KY,4)
6002 LX=RND(30)+25: LY=RND(15)+10 : SET(LX,LY,4)
6003 RETURN
7000 FOR D=1 TO 62
7020 SET(D,6,3) : SET(D,30,3)
7040 NEXT D
7050 FOR H=6 TO 30
7060 SET(1,H,3) : SET(62,H,3)
7080 NEXT H
7090 PRINT@0, "SCORE: 0" : RETURN
8000 FOR G=0 TO 32 STEP 4
8020 SOUND 127+G,1
8030 NEXT G
8040 RETURN
9000 RESET(JX,JY) : MV=RND(255)
9030 IF MV<64 THEN LET JY=JY-1
9040 IF MV>64 AND MV<128 THEN LET JY=JY+1
9050 IF MV>128 AND MV<192 THEN LET JX=JX-1
9060 IF MV>192 THEN LET JX=JX+1
9070 IF JX>60 THEN LET JX=JX-1
9080 IF JY>28 THEN LET JY=JY-1
9090 IF JX<3 THEN LET JX=JX+1
9100 IF JY<8 THEN LET JY=JY+1
9110 SET(JX,JY,4) : RESET(KX,KY) : MV=RND(255)
9140 IF MV<64 THEN LET KY=KY-1
9150 IF MV>64 AND MV<128 THEN LET KY=KY+1
9160 IF MV>128 AND MV<192 THEN LET KX=KX-1
9170 IF MV>192 THEN LET KX=KX+1
9180 IF KX>60 THEN LET KX=KX-1
9190 IF KY>28 THEN LET KY=KY-1
9200 IF KX<3 THEN LET KX=KX+1
9210 IF KY<8 THEN LET KY=KY+1
9220 SET(KX,KY,4) : RESET(LX,LY) : MV=RND(255)
9250 IF MV<64 THEN LET LY=LY-1
9260 IF MV>64 AND MV<128 THEN LET LY=LY+1
9270 IF MV>128 AND MV<192 THEN LET LX=LX-1
9280 IF MV>192 THEN LET LX=LX+1
9290 IF LX>60 THEN LET LX=LX-1
9300 IF LY>28 THEN LET LY=LY-1
9310 IF LX<3 THEN LET LX=LX+1
9320 IF LY<8 THEN LET LY=LY+1
9330 SET(LX,LY,4) : RETURN
10000 CLS0 : PRINT@106," BLOCKWAR! "; : PRINT@234," (C)  1982 "; : PRINT@294," LABYRINTH SYSTEMS ";
10001 FOR T = 64 TO 192 STEP 8
10002 SOUND T,1
10003 NEXT T
10004 PRINT@418," PRESS ANY KEY TO CONTINUE ";
10050 Q$=INKEY$ : IF Q$="" THEN GOTO 10050
10060 CLS0 : RETURN
10100 CLS0 : PRINT "HIGH SCORE: ";W : PRINT@140,"WINRAR!!!";
10117 FOR T = 64 TO 192 STEP 8
10118 SOUND T,1
10119 NEXT T
10120 PRINT@356,"PRESS ANY KEY TO CONTINUE";
10130 Q$=INKEY$ : IF Q$="" THEN GOTO 10130
10140 CLS0 : GOTO 1000
